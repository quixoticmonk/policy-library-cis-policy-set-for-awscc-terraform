# This policy requires resources of type `awscc_rds_db_instance` have attribute "storage_encrypted" set to true.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                    "rds-encryption-at-rest-enabled",
	"message":                        "Attribute 'storage_encrypted' must be set to true for 'awscc_rds_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-3 for more details.",
	"resource_awscc_rds_db_instance": "awscc_rds_db_instance",
	"storage_encrypted":              "storage_encrypted",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_awscc_rds_db_instance).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.storage_encrypted", false) is true
})

summary = {
	"policy_name": "rds-encryption-at-rest-enabled",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
